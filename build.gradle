buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "io.ratpack:ratpack-gradle:0.9.15"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.0.2'
    }
}

version = '1.0.1'

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "idea"

repositories {
    jcenter()
    maven { url 'http://clinker.netty.io/nexus/content/repositories/snapshots' }
}

dependencies {
    compile 'io.ratpack:ratpack-jackson:0.9.15'

    runtime "org.slf4j:slf4j-simple:1.7.5"
    compile "com.pi4j:pi4j-core:1.2"
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.4'

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0", {
        exclude module: "groovy-all"
    }
}

run {
    jvmArgs "-DfakePi=true"
//    debug true
}

task buildUI(type:Exec) {
    workingDir 'src/ratpack/public'
    commandLine 'npm', 'run', 'build'
}

tasks.shadowJar.dependsOn(buildUI)

shadowJar {
    exclude 'LICENSE*'
    exclude 'NOTICE*'
    exclude 'overview*'
    exclude 'README*'
    exclude 'public/node_modules'
}

// create .deb package - needs fpm installed (gem install fpm)
task deb(dependsOn: 'shadowJar') << {
    delete {
        file 'build/debian'
    }
    copy {
        from file("build/libs/goldilocks-${version}-all.jar")
        into 'build/debian/var/lib/goldilocks'
        rename ("goldilocks-${version}-all.jar", 'goldilocks.jar')
    }
    copy {
        from file("src/linux/dist")
        into 'build/debian/var/lib/goldilocks/'
    }
    copy {
        from file("src/linux/debian")
        into 'build/debian/etc/init.d/'
        include('init.sh')
        rename ('init.sh', 'goldilocks')
    }
    copy {
        from file("src/linux/debian/default")
        into 'build/debian/etc/default'
        rename ('default', 'goldilocks')
    }

    def fpm = ("fpm -s dir -t deb -n goldilocks -v ${version} -a noarch " +
            "--config-files /etc/default/goldilocks " +
            "--after-install src/linux/debian/postinst " +
            "--before-remove src/linux/debian/prerm " +
            "--deb-user root --deb-group root " +
            "-f -p build/distributions/goldilocks-${version}.deb " +
            "-C build/debian/")
            .split(" ") as List
    fpm << "--license" << "GPL V3"
    fpm << "--description" << "Raspberry Pi based temperature logging and control for home brewing"
    fpm << "."
    exec {
        commandLine fpm
    }
}
